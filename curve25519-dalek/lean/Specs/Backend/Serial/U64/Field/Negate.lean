module
public import Curve25519
public import Curve25519Dalek.Funs
public import Specs.Backend.Serial.U64.Defs
public import Subtle
public import Specs.Backend.Serial.U64.Field.Reduce
public import Specs.Backend.Serial.U64.Field.SixteenP
public import Specs.Lemmas.AsNat
public section

open Aeneas Aeneas.Std Result Aeneas.Std.WP curve25519
open curve25519_dalek.backend.serial.u64.field (FieldElement51)

namespace curve25519_dalek.backend.serial.u64.field.FieldElement51

/-- The closure of `negate`: limb `i` of `16 p - self`. -/
@[step]
theorem negate.closure.Insts.CoreOpsFunctionFnMutTupleUsizeU64.call_mut_spec
    (c : negate.closure) (i : Usize) (hi : i.val < 5) (hc : c[i.val]!.val < 2 ^ 54) :
    negate.closure.Insts.CoreOpsFunctionFnMutTupleUsizeU64.call_mut c i
      ⦃ (r : U64) (c' : negate.closure) =>
        r.val + c[i.val]!.val = SIXTEEN_P[i.val]!.val ∧ c' = c ⦄ := by
  have h16 := SIXTEEN_P_spec.2.1 i.val hi
  simp (disch := scalar_tac) only [Array.getElem!_Nat_eq, getElem!_pos] at hc h16 ⊢
  unfold negate.closure.Insts.CoreOpsFunctionFnMutTupleUsizeU64.call_mut
  step*

@[step]
theorem negate_spec (self : FieldElement51) (hself : ∀ i < 5, self[i]!.val < 2 ^ 54) :
    negate self ⦃ (r : FieldElement51) =>
      (r.asNat + self.asNat) % p = 0 ∧ ∀ i < 5, r[i]!.val < 2 ^ 52 ⦄ := by
  unfold negate
  apply spec_bind (core.array.from_fn_spec 5#usize _ self (fun _ c => c = self)
    (fun i x => x.val + self[i]!.val = SIXTEEN_P[i]!.val) rfl fun i c hi hc => ?_)
  · rintro a ha
    have hlimbs : ∀ i < 5, a[i]!.val + self[i]!.val = SIXTEEN_P[i]!.val := fun i hi => by
      rw [Array.getElem!_Nat_eq, getElem!_pos a.val i (by simpa using hi)]
      exact ha i (by simpa using hi)
    step as ⟨r, hr, hr_lt, hr_2p⟩
    refine ⟨?_, hr_lt⟩
    have hsum : FieldElement51.asNat a + self.asNat = FieldElement51.asNat SIXTEEN_P :=
      Array.asNat_add_eq 51 a self SIXTEEN_P hlimbs
    rw [Nat.add_mod, hr, ← Nat.add_mod, hsum, SIXTEEN_P_spec.1, Nat.mul_mod_left]
  · subst hc
    exact negate.closure.Insts.CoreOpsFunctionFnMutTupleUsizeU64.call_mut_spec c i hi
      (hself i.val hi)

end curve25519_dalek.backend.serial.u64.field.FieldElement51
