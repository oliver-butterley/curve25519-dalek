module
public import Curve25519
public import Curve25519Dalek.Funs
public import Specs.Backend.Serial.U64.Defs
public import Subtle
public import Specs.Backend.Serial.U64.Scalar.SquareInternal
public import Specs.Backend.Serial.U64.Scalar.MulInternal
public import Specs.Backend.Serial.U64.Scalar.MontgomeryReduce
public import Specs.Backend.Serial.U64.Constants.RR
public import Specs.Backend.Serial.U64.Scalar.Lemmas
public section

open Aeneas Aeneas.Std Result Aeneas.Std.WP curve25519
open Curve25519Dalek.backend.serial.u64.scalar (Scalar52)

namespace Curve25519Dalek.backend.serial.u64.scalar.Scalar52

@[step]
theorem square_spec (self : Scalar52) (hself : ∀ i < 5, self[i]!.val < 2 ^ 52)
    (hself' : self.asNat ^ 2 < montgomeryRadix * L) :
    square self ⦃ (r : Scalar52) =>
      r.asNat = self.asNat ^ 2 % L ∧ ∀ i < 5, r[i]!.val < 2 ^ 52 ⦄ := by
  unfold square
  obtain ⟨hRR, hRR_lt⟩ := constants.RR_spec
  generalize hR2 : montgomeryRadix ^ 2 % L = R2 at hRR
  have hR2L : R2 < L := hR2 ▸ Nat.mod_lt _ L_pos
  step with square_internal_spec self hself as ⟨aa, haa_eq, haa_lt⟩
  step with montgomery_reduce_spec aa (fun i hi => (haa_lt i hi).trans (by decide))
    (haa_eq ▸ hself') as ⟨t, ht, htL, ht_lt⟩
  have htRR : t.asNat * constants.RR.asNat < montgomeryRadix * L :=
    Nat.mul_lt_mul'' (htL.trans L_lt_montgomeryRadix) (hRR ▸ hR2L)
  step with mul_internal_spec t constants.RR ht_lt hRR_lt as ⟨tRR, htRR_eq, htRR_lt⟩
  step with montgomery_reduce_spec tRR (fun i hi => (htRR_lt i hi).trans (by decide))
    (htRR_eq ▸ htRR) as ⟨r, hr, hrL, hr_lt⟩
  refine ⟨?_, hr_lt⟩
  rw [htRR_eq, hRR, ← hR2] at hr
  have h := mod_L_of_mul_montgomeryRadix_eq_mul_RR hr
  rw [Nat.mod_eq_of_lt hrL] at h
  rw [h, ht, haa_eq]

end Curve25519Dalek.backend.serial.u64.scalar.Scalar52
