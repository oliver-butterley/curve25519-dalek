module
public import Curve25519
public import Curve25519Dalek.Funs
public import Specs.Backend.Serial.U64.Defs
public import Subtle
public import Specs.Field.InternalInvertBatch
public import Specs.Backend.Serial.U64.Field.One
public section

open Aeneas Aeneas.Std Result Aeneas.Std.WP curve25519
open curve25519_dalek.backend.serial.u64.field (FieldElement51)

namespace curve25519_dalek.field.FieldElement51

@[step]
theorem invert_batch_spec {N : Usize} (inputs : Array FieldElement51 N)
    (hinputs : ∀ i < N.val, ∀ j < 5, (inputs[i]!)[j]!.val < 2 ^ 54) :
    invert_batch inputs ⦃ (r : Array FieldElement51 N) =>
      ∀ i < N.val,
        (inputs[i]!.asNat % p = 0 → r[i]! = inputs[i]!) ∧
        (inputs[i]!.asNat % p ≠ 0 → r[i]!.asNat * inputs[i]!.asNat % p = 1 ∧
          ∀ j < 5, (r[i]!)[j]!.val < 2 ^ 52) ⦄ := by
  unfold invert_batch
  step as ⟨one, hone, honeb⟩
  step as ⟨s, back, hs, hback⟩
  step as ⟨s1, back1, hs1, hback1⟩
  step as ⟨s2, scratch, hs2len, hs2⟩
  have hsN : s.length = N.val := by
    simp only [Slice.length, hs]
    exact inputs.property
  have hget : ∀ i : ℕ, (back s2)[i]! = s2[i]! := by
    intro i
    rw [hback, Array.getElem!_Nat_eq, Array.from_slice_val _ _ (by simp [hs2len, hsN]),
      ← Slice.getElem!_Nat_eq]
  have hin : ∀ i : ℕ, inputs[i]! = s[i]! := by
    intro i
    rw [Array.getElem!_Nat_eq, Slice.getElem!_Nat_eq, hs]
  intro i hi
  rw [hget, hin]
  exact hs2 i (hsN ▸ hi)

end curve25519_dalek.field.FieldElement51
